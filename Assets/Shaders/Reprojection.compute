// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Reprojection

// Group size
#define size_x 24
#define size_y 24

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
Texture2D<float4> MotionVectorOutput;
Texture2D<float4> CurrentFrame;
Texture2D<float4> GBufferNormals;
Texture2D<float4> Position;
Texture2D<float4> LastFrame;
Texture2D<float4> PrevGBufferNormals;
Texture2D<float4> PrevGBufferPosition;
RWTexture2D<float4> ReprojectedOutput;

// Declare one thread for each texel of the current block size.
[numthreads(size_x, size_y, 1)]
void Reprojection(uint3 dispatchThreadId : SV_DispatchThreadID)
{
    float4 motionVector = MotionVectorOutput.Load(dispatchThreadId);
    float4 currentFrameColor = CurrentFrame.Load(dispatchThreadId);

    // if primary ray miss, MotionVectorOutput.w is set to 0 -> there are no data to reproject
    if (motionVector.w < 0.01)
    {
        //ReprojectedOutput[dispatchThreadId.xy] = float4(0, 0, 1, 1); //currentFrameColor;
        ReprojectedOutput[dispatchThreadId.xy] = currentFrameColor;
        return;
    }

    // calculate last frame position based on motion vector and get last frame color
    float4 currentNormalsandIds = GBufferNormals.Load(dispatchThreadId);
    int3 lastFramePosition = int3(dispatchThreadId.x - motionVector.x, dispatchThreadId.y - motionVector.y, 0);
    float4 lastFrameColor = LastFrame.Load(lastFramePosition);
    float4 currentWorldPosition = Position.Load(dispatchThreadId);
    float4 lastFrameWorldPosition = PrevGBufferPosition.Load(lastFramePosition);
    float4 distance = currentWorldPosition - lastFrameWorldPosition;
    float t = dot(distance, distance);
    
    // check normals and object IDs (object IDs are saved in currentNormalsandIds.w)
    float4 normalsCheck = abs(currentNormalsandIds - PrevGBufferNormals.Load(lastFramePosition));
    if (normalsCheck.x < 0.005 && normalsCheck.y < 0.005 && normalsCheck.z < 0.005 && normalsCheck.w < 0.1 && t < 0.5)
    {
        ReprojectedOutput[dispatchThreadId.xy] = 0.2f * currentFrameColor + 0.8f * lastFrameColor;
    }
    else
    {
        //ReprojectedOutput[dispatchThreadId.xy] = float4(1, 0, 0, 1); //currentFrameColor;
        ReprojectedOutput[dispatchThreadId.xy] = currentFrameColor;
    }
}
