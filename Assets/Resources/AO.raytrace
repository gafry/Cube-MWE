#pragma max_recursion_depth 11

#include "./../Shaders/Common.hlsl"

// Input
Texture2D<float4> GBufferNormalsAndMaterials;
Texture2D<float4> GBufferPositionsAndDepth;
// Output
RWTexture2D<float4> _OutputTarget;
int _FrameCounter;
float AOCoef;

float shootAmbientOcclusionRay( float3 orig, float3 dir, float minT, float maxT )
{
	// Setup ambient occlusion ray and payload
	RayPayloadAO rayPayloadAO = { 0.0f };
	RayDesc rayDescriptor;
	rayDescriptor.Origin    = orig;
	rayDescriptor.Direction = dir;  
	rayDescriptor.TMin      = minT;
	rayDescriptor.TMax      = maxT;             

	// Trace ray
	TraceRay(_AccelerationStructure,
		RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH | RAY_FLAG_SKIP_CLOSEST_HIT_SHADER,
		0xFF, 0, 1, 0, rayDescriptor, rayPayloadAO);

	// Return payload
	return rayPayloadAO.AOValue;
}

[shader("raygeneration")]
void AORaygenShader()
{
	const uint2 dispatchIdx = DispatchRaysIndex().xy;
	const uint2 dispatchDim = DispatchRaysDimensions();

	// Get random seed based on frame counter
	uint randSeed = initRand(dispatchIdx.x + dispatchIdx.y * dispatchDim.x, _FrameCounter, 16);

	// Load the position and normal from our g-buffer
	float3 worldNorm = GBufferNormalsAndMaterials[dispatchIdx].xyz;
	float4 worldPos = GBufferPositionsAndDepth[dispatchIdx];

	// number of rays
	int numberOfRays = 4;

	// default AO
	float ambientOcclusion = 1.0f;

	// Our G-buffer stores 0 in worldPos.w for background pixels; only shoot an AO ray elsewhere
	if (worldPos.w != 0.0f || worldPos.w < 0.5f)
	{
		// Start accumulating from zero if we don't hit the background
		ambientOcclusion = 0.0f;

		float rayLength = 0.15f * AOCoef;

		for (int i = 0; i < numberOfRays; i++)
		{
			// Sample cosine-weighted hemisphere around surface normal to pick a random ray direction
			float3 worldDir = getCosHemisphereSample(randSeed, worldNorm);

			// Shoot our ambient occlusion ray and update the value we'll output with the result
			ambientOcclusion += shootAmbientOcclusionRay(worldPos.xyz, worldDir, 3e-5f, rayLength);
		}
	} 
	else
	{
		_OutputTarget[dispatchIdx] = float4(1.0f, 1.0f, 1.0f, 1.0f);
		return;
	}

	float ResultAO = ambientOcclusion / float(numberOfRays);
	float4 color = float4(ResultAO, ResultAO, ResultAO, 1.0f);

	_OutputTarget[dispatchIdx] = color;
}

[shader("miss")]
void MissShader(inout RayPayloadAO rayPayloadAO : SV_RayPayload)
{
	rayPayloadAO.AOValue = 1.0f;
}